; -- installer.iss --
; Inno Setup script template for creating the dvdisaster setup.exe

[Setup]
AppName=dvdisaster
AppVerName=dvdisaster-%version%
AppMutex=dvdisaster
DefaultDirName={pf}\dvdisaster
DefaultGroupName=dvdisaster
UninstallDisplayIcon={app}\dvdisaster.exe
Compression=lzma
SolidCompression=yes
SourceDir=dvdisaster-%version%
OutputDir=.
OutputManifestFile=manifest.txt
OutputBaseFilename=dvdisaster-%version%-setup
AllowNoIcons=yes
;SetupIconFile=setup.ico

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "cs"; MessagesFile: "compiler:Languages/Czech.isl"
Name: "de"; MessagesFile: "compiler:Languages/German.isl"

[Dirs]
Name: "{app}\documentation\local\images"

[Files]
Source: "CHANGELOG"; DestDir: "{app}"
Source: "COPYING"; DestDir: "{app}"
Source: "CREDITS.*"; DestDir: "{app}"
Source: "TODO"; DestDir: "{app}"
Source: "*.exe"; DestDir: "{app}"
Source: "*.dll"; DestDir: "{app}"
Source: "lib\locale\cs\LC_MESSAGES\*"; DestDir: "{app}\lib\locale\cs\LC_MESSAGES"
Source: "lib\locale\de\LC_MESSAGES\*"; DestDir: "{app}\lib\locale\de\LC_MESSAGES"
Source: "lib\locale\it\LC_MESSAGES\*"; DestDir: "{app}\lib\locale\it\LC_MESSAGES"
;Source: "etc\gtk-2.0\*"; DestDir: "{app}\etc\gtk-2.0"
Source: "etc\pango\*"; DestDir: "{app}\etc\pango"
Source: "locale\cs\LC_MESSAGES\dvdisaster.mo"; DestDir: "{app}\locale\cs\LC_MESSAGES"
Source: "locale\de\LC_MESSAGES\dvdisaster.mo"; DestDir: "{app}\locale\de\LC_MESSAGES"
Source: "locale\it\LC_MESSAGES\dvdisaster.mo"; DestDir: "{app}\locale\it\LC_MESSAGES"
Source: "documentation\images-common.pngpack"; DestDir: "{app}\documentation"
Source: "documentation\pngpack.exe"; DestDir: "{app}\documentation"
Source: "documentation\pubkey.asc"; DestDir: "{app}\documentation"
Source: "documentation\images\*"; DestDir: "{app}\documentation\images"
Source: "documentation\images-cs.pngpack"; DestDir: "{app}\documentation"; Languages: cs
Source: "documentation\cs\*"; DestDir: "{app}\documentation\local"; Languages: cs
Source: "documentation\images-de.pngpack"; DestDir: "{app}\documentation"; Languages: de
Source: "documentation\de\*"; DestDir: "{app}\documentation\local"; Languages: de
Source: "documentation\images-en.pngpack"; DestDir: "{app}\documentation"; Languages: en
Source: "documentation\en\*"; DestDir: "{app}\documentation\local"; Languages: en

[Icons]
Name: "{group}\dvdisaster"; Filename: "{app}\dvdisaster-win.exe"; WorkingDir: "{app}"
Name: "{group}\Uživatelská pøíruèka"; Filename: "{app}\documentation\local\index.html"; Languages: cs
Name: "{group}\Handbuch"; Filename: "{app}\documentation\local\index.html"; Languages: de
Name: "{group}\Manual"; Filename: "{app}\documentation\local\index.html"; Languages: en
Name: "{group}\Uninstall"; Filename: "{uninstallexe}"
Name: "{userdesktop}\dvdisaster"; Filename: "{app}\dvdisaster-win.exe"; WorkingDir: "{app}"

[Run]
Filename: "{app}\documentation\pngpack.exe"; Parameters: "{app}\documentation\images-common.pngpack"; WorkingDir: "{app}\documentation\images"; Flags: runhidden 
Filename: "{app}\documentation\pngpack.exe"; Parameters: "{app}\documentation\images-cs.pngpack"; WorkingDir: "{app}\documentation\local\images"; Flags: runhidden; Languages: cs
Filename: "{app}\documentation\pngpack.exe"; Parameters: "{app}\documentation\images-de.pngpack"; WorkingDir: "{app}\documentation\local\images"; Flags: runhidden; Languages: de
Filename: "{app}\documentation\pngpack.exe"; Parameters: "{app}\documentation\images-en.pngpack"; WorkingDir: "{app}\documentation\local\images"; Flags: runhidden; Languages: en

[UninstallDelete]
Type: files; Name:"{app}\.dvdisaster"
Type: filesandordirs; Name:"{app}\documentation\images"
Type: filesandordirs; Name:"{app}\documentation\local\images"
Type: files; Name:"{app}\medium.iso"
Type: files; Name:"{app}\medium.ecc"

